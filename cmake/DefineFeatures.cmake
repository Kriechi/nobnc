include(CMakeDependentOption)

option(FEATURE_IPV6 "Enable IPv6 support" ON)
cmake_dependent_option(HAVE_IPV6 "Enable IPv6 support" ON "FEATURE_IPV6" OFF)
mark_as_advanced(HAVE_IPV6)

option(FEATURE_SSL "Enable SSL support" ON)
if(FEATURE_SSL)
    find_package(OpenSSL)
    if(OPENSSL_FOUND)
        list(APPEND NO_INCLUDEDIRS ${OPENSSL_INCLUDE_DIRS})
        list(APPEND NO_LIBRARIES ${OPENSSL_LIBRARIES})
    endif()
endif()
cmake_dependent_option(HAVE_LIBSSL "Enable SSL support" ON "FEATURE_SSL;OPENSSL_FOUND" OFF)
cmake_dependent_option(HAVE_LIBCRYPTO "Enable libcrypto support" ON "FEATURE_SSL;OPENSSL_FOUND" OFF)
mark_as_advanced(HAVE_LIBSSL HAVE_LIBCRYPTO)

option(FEATURE_ICU "ICU support" ON)
if(FEATURE_ICU)
    find_package(ICU)
    if(ICU_FOUND)
        list(APPEND NO_INCLUDEDIRS ${ICU_INCLUDE_DIRS})
        list(APPEND NO_LIBRARIES ${ICU_LIBRARIES})
    endif()
endif()
cmake_dependent_option(HAVE_ICU "Enable ICU support" ON "FEATURE_ICU;ICU_FOUND" OFF)
mark_as_advanced(HAVE_ICU)

option(FEATURE_ZLIB "Enable zlib support" ON)
if(FEATURE_ZLIB)
    find_package(ZLIB)
    if(ZLIB_FOUND)
        list(APPEND NO_INCLUDEDIRS ${ZLIB_INCLUDE_DIRS})
        list(APPEND NO_LIBRARIES ${ZLIB_LIBRARIES})
    endif()
endif()
cmake_dependent_option(HAVE_ZLIB "Enable zlib support" ON "FEATURE_ZLIB;ZLIB_FOUND" OFF)
mark_as_advanced(HAVE_ZLIB)

option(FEATURE_THREADS "Enable threads support" ON)
if(FEATURE_THREADS)
    set(CMAKE_THREAD_PREFER_PTHREAD 1)
    find_package(Threads)
    if(Threads_FOUND)
        set(HAVE_PTHREAD 1)
        list(APPEND NO_LIBRARIES ${CMAKE_THREAD_LIBS_INIT})
    endif()
endif()
cmake_dependent_option(HAVE_THREADS "Enable thread support" ON "FEATURE_THREADS;Threads_FOUND" OFF)
mark_as_advanced(HAVE_THREADS)

option(FEATURE_POLL "Use poll() instead of select()" ON)
cmake_dependent_option(CSOCK_USE_POLL "Use poll() instead of select()" ON "FEATURE_POLL" OFF)
mark_as_advanced(CSOCK_USE_POLL)

option(FEATURE_LARGEFILE "Enable large file support" ON)
cmake_dependent_option(_LARGE_FILES "Enable large file support" ON "FEATURE_LARGEFILE" OFF)
mark_as_advanced(_LARGE_FILES)

option(FEATURE_CYRUS "Enable Cyrus support" ON)
if(FEATURE_CYRUS)
    find_package(Sasl2)
    if(SASL2_FOUND)
        list(APPEND NO_INCLUDEDIRS ${SASL2_INCLUDE_DIR})
        list(APPEND NO_LIBRARIES ${SASL2_LIBRARIES})
    endif()
endif()
cmake_dependent_option(HAVE_CYRUS "Enable Cyrus support" ON "FEATURE_CYRUS;SASL2_FOUND" OFF)
mark_as_advanced(HAVE_CYRUS)

option(DEVELOPER "Enable developer build" OFF)
if(DEVELOPER)
    set(CMAKE_BUILD_TYPE "Debug")
elseif(NOT CMAKE_BUILD_TYPE)
    set(CMAKE_BUILD_TYPE "Release")
endif()

cmake_dependent_option(FEATURE_GIT "Enable Git" ON "EXISTS ${PROJECT_SOURCE_DIR}/.git" OFF)
if(FEATURE_GIT)
    find_package(Git QUIET)
endif()
